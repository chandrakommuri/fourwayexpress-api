plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.0'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.openapi.generator' version '7.4.0'
}

group = 'com.fourwayexpress'
version = '1.0.0-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'io.swagger.core.v3:swagger-annotations:2.2.15'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

	implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
	implementation 'org.hibernate.validator:hibernate-validator:8.0.1.Final'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

sourceSets {
	main {
		java {
			srcDir layout.buildDirectory.dir("generated/src/main/java")
		}
	}
}

openApiGenerate {
	generatorName = "spring"
	inputSpec = "$rootDir/src/main/openapi/fourwayexpress-api.yaml".toString()
	outputDir = layout.buildDirectory.dir("generated").get().asFile.path
	apiPackage = "com.fourwayexpress.api"
	modelPackage = "com.fourwayexpress.model"
	invokerPackage = "com.fourwayexpress.invoker"
	configOptions = [
			interfaceOnly: "true",
			skipDefaultInterface: "true",
			useTags: "true",
			dateLibrary: "java8",
			useJakartaEe: "true",
			additionalModelTypeAnnotations: "@lombok.Data @lombok.NoArgsConstructor @lombok.AllArgsConstructor @lombok.Builder"
	]
}

tasks.named('compileJava') {
	dependsOn tasks.named('openApiGenerate')
}

tasks.named('jar') {
	enabled = false
}

tasks.named('test') {
	useJUnitPlatform()
}
